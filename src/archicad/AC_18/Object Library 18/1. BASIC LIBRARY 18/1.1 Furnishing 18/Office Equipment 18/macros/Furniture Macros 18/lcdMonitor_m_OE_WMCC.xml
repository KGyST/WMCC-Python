<Symbol IsArchivable="no" IsPlaceable="no" MainGUID="74ABE416-816A-4C4C-AAEF-E14C3966FEBC" MigrationValue="Normal" Owner="1196638531" Signature="1196644685" Version="34">
<Ancestry SectVersion="1" SectionFlags="0" SubIdent="0" Template="no">
	<MainGUID>F938E33A-329D-4A36-BE3E-85E126820996</MainGUID>
	<MainGUID>103E8D2C-8230-42E1-9597-46F84CCE28C0</MainGUID>
	<MainGUID>C76BC373-89AE-4368-A490-BB2485825242</MainGUID>
	<MainGUID>8D1653CC-B1FA-4DDD-AADD-7580568596CF</MainGUID>
	<MainGUID>46085A21-14EF-4EA4-92C3-CD8E33A02699</MainGUID>
</Ancestry>

<CalledMacros SectVersion="2" SectionFlags="0" SubIdent="0">
	<Macro>
		<MName><![CDATA["Resize_A_B_ZZYZX"]]></MName>
		<MainGUID>33A17ABE-882E-11D6-95A3-00039318F8B6</MainGUID>
	</Macro>
	<Macro>
		<MName><![CDATA["FM_types_OE_WMCC"]]></MName>
		<MainGUID>16A8E793-F7FC-4F23-86F6-8C73CEBE740F</MainGUID>
	</Macro>
</CalledMacros>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_3D SectVersion="20" SectionFlags="0" SubIdent="0"><![CDATA[

if gs_detlevel_3D_m = 0 then end

if NOT(gs_shadow) then shadow off

if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 or gs_detlevel_3D_m = 1 then 		!! for Simple Model and in 3D Windoow
	if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 then
		iResol = 3
	else
		iResol = 8
	endif
else
	iResol = gs_resol
endif

resol iResol

if iMonitorType = 1 then

	frameThickness = 0.015
	bottomFrameThickness = 0.015
	tp = zzyzx/7
	unID = 1

	if NOT(bPartOfLayout) then
		if bUnitSizing then
			HOTSPOT A, 0, 0, unID :unID=unID+1
			HOTSPOT A, B, 0, unID :unID=unID+1
			HOTSPOT 0, B, 0, unID :unID=unID+1
			HOTSPOT 0, 0, 0, unID :unID=unID+1

			HOTSPOT A, 0, zzyzx, unID :unID=unID+1
			HOTSPOT A, B, zzyzx, unID :unID=unID+1
			HOTSPOT 0, B, zzyzx, unID :unID=unID+1
			HOTSPOT 0, 0, zzyzx, unID :unID=unID+1

			add 0,b/2-frameThickness ,zzyzx/5
			rotx -5
			HOTSPOT frameThickness, 0, bottomFrameThickness , unID, screenSize, 1+128 :unID=unID+1
			HOTSPOT 0, 0, bottomFrameThickness , unID, screenSize, 3+128 :unID=unID+1
			HOTSPOT A-frameThickness , 0, zzyzx-tp, unID, screenSize, 2 :unID=unID+1
			del 2
		else
			HOTSPOT 0, 0, 0, unID, A, 1+256 :unID=unID+1
			HOTSPOT -1, 0, 0, unID, A, 3+128 :unID=unID+1
			HOTSPOT A, 0, 0, unID, A, 2 :unID=unID+1

			HOTSPOT 0, B, 0, unID, A, 1+256 :unID=unID+1
			HOTSPOT -1, B, 0, unID, A, 3+128 :unID=unID+1
			HOTSPOT A, B, 0, unID, A, 2 :unID=unID+1

			HOTSPOT 0, 0, zzyzx, unID, A, 1+256 :unID=unID+1
			HOTSPOT -1, 0, zzyzx, unID, A, 3+128 :unID=unID+1
			HOTSPOT A, 0, zzyzx, unID, A, 2 :unID=unID+1

			HOTSPOT 0, B, zzyzx, unID, A, 1+256 :unID=unID+1
			HOTSPOT -1, B, zzyzx, unID, A, 3+128 :unID=unID+1
			HOTSPOT A, B, zzyzx, unID, A, 2 :unID=unID+1

			HOTSPOT 0, 0, 0, unID, B, 1+256 :unID=unID+1
			HOTSPOT 0, -1, 0, unID, B, 3+128 :unID=unID+1
			HOTSPOT 0, B, 0, unID, B, 2 :unID=unID+1

			HOTSPOT A, 0, 0, unID, B, 1+256 :unID=unID+1
			HOTSPOT A, -1, 0, unID, B, 3+128 :unID=unID+1
			HOTSPOT A, B, 0, unID, B, 2 :unID=unID+1

			HOTSPOT 0, 0, 0, unID, zzyzx, 1+128 :unID=unID+1
			HOTSPOT 0, 0, -1, unID, zzyzx, 3+128 :unID=unID+1
			HOTSPOT 0, 0, zzyzx, unID, zzyzx, 2 :unID=unID+1

			HOTSPOT A, 0, 0, unID, zzyzx, 1+128 :unID=unID+1
			HOTSPOT A, 0, -1, unID, zzyzx, 3+128 :unID=unID+1
			HOTSPOT A, 0, zzyzx, unID, zzyzx, 2 :unID=unID+1

			HOTSPOT 0, B, 0, unID, zzyzx, 1+128 :unID=unID+1
			HOTSPOT 0, B, -1, unID, zzyzx, 3+128 :unID=unID+1
			HOTSPOT 0, B, zzyzx, unID, zzyzx, 2 :unID=unID+1

			HOTSPOT A, B, 0, unID, zzyzx, 1+128 :unID=unID+1
			HOTSPOT A, B, -1, unID, zzyzx, 3+128 :unID=unID
			HOTSPOT A, B, zzyzx, unID, zzyzx, 2 :unID=unID+1
		endif
	endif

	DEFINE MATERIAL "pictmat" 2,
		1,1,1

	MATERIAL "pictmat"

	if gs_detlevel_3D=`Off` then end
	IF gs_detlevel_3D=`Simple` then gs_resol=3

	IF gs_shadow=0 THEN SHADOW OFF

	DEFINE MATERIAL "ooo" 20,
				 1,          1,          1,
		 0.86,  0.81,  0.09,     0,    2.2,     0,
		  0.990005,   0.990005,          1,
				 0,          0,          0,
				 0,     0,    -1,
		0

	pen gs_cont_pen

	material box_mat
	add 0,b/2-.015,zzyzx/7

	rotx 85
	cutform 5, 1, 1,
		0, 0, 1, -0.005,
		0.015, 0.015, 1 + 2 + 4 + 8 + 16,
		a-0.015, 0.015, 1 + 2 + 4 + 8 + 16,
		a-0.015, zzyzx*6/7-0.015, 1 + 2 + 4 + 8 + 16,
		0.015, zzyzx*6/7-0.015, 1 + 2 + 4 + 8 + 16,
		0.015, 0.015, -1
	del 1

	rotx -5
	prism_ 5,zzyzx*6/7,
			0,0,15,
			a,0,15,
			a,.03,15,
			0,.03,15,
			0,0,-1

	cutend

	rotx 90
	if pic=1 then
		material "ooo"
		add 0.015, 0.015,0

		if gs_picture_custom=0 then
			picture "pc_desktop_OE_WMCC", a-.03,zzyzx*6/7-2*0.015,0
		else
			If gs_picture_name <> "" Then
				picture gs_picture_name, a-.03,zzyzx*6/7-2*0.015, 0
			Else
				picture "pc_desktop_OE_WMCC", a-.03,zzyzx*6/7-2*0.015,0
			Endif
		endif

		del 1
	else
		!!MATERIAL  "gls"
		MATERIAL dispmat
		prism_ 	5,     0,
				0.015, 0.015, 15,
				a-0.015, 0.015, 15,
				a-0.015, zzyzx*6/7-0.015, 15,
				0.015, zzyzx*6/7-0.015, 15,
				0.015, 0.015, -1
	endif

	del 1

	material feetmat
	add a-.15,-.002,.003

	for i=1 to 4
		prism_	5,     0.004,
				0,0,15,
				0.025,0,15,
				0.025,0.02,15,
				0,0.02,15,
				0,0,-1

		addx 0.035
	next i

	del 5

	del 2

	material feetmat

	add a/4,0,0

	prism_	5,.01,
			0,0,15,
			a/2,0,15,
			a/2,b,15,
			0,b,15,
			0,0,-1

	del 1

	add 0,b/2+.015,zzyzx*2/7-0.015

	rotx -95
	rotz -90

	HPRISM_ feetmat,feetmat,feetmat,0,4,.0315,0,.0315,1,
			0,.025,79,
			zzyzx*5/7,.025,79,
			zzyzx*5/7,a-.025,79,
			0,a-.025,79
	del 3

	add a/2-(a/12),b/2+.028,zzyzx*2/7-.008-0.015
	roty 90

	cylind a/6,.015
	del 2

	add a/2-(a/12),b-.02,0
	rotx  atn((b/2-2*0.015)/(zzyzx*2/7))     !20
	prism_ 5,SQR((b/2-2*0.015)^2+(zzyzx*2/7-2*.015)^2),
			0,0,15,
			a/6,0,15,
			a/6,.02,15,
			0,.02,15,
			0,0,-1
	del 2
endif

if iMonitorType = 2 then

	resol gs_resol * (gs_detlevel_3D_m = 2) + 8 * (gs_detlevel_3D_m = 1)
	PEN gs_cont_pen

	!! DEFINE MATERIALS !!
	define empty_fill "emptyFill"

	if gs_picture_custom and gs_picture_name <> "" then
		define texture "custDesktop" gs_picture_name, screenSizeX, screenSizeY, 0, 0
		define material "custDesktop" 20,
			0.8, 0.8, 0.8,
			0.58, 0.85, 0.2, 0,
			50,
			0,
			0.878401, 0.513481, 0.412253,
			0, 0, 0,
			0,
			ind(fill, "emptyFill"), 1,
			ind(texture, "custDesktop")
	else
		define texture "pcDesktop" "pc_desktop_OE_WMCC", screenSizeX, screenSizeY, 0, 0
		file_dependence "pc_desktop_OE_WMCC"
		define material "pcDesktop" 20,
			0.8, 0.8, 0.8,
			0.58, 0.85, 0.2, 0,
			50,
			0,
			0.878401, 0.513481, 0.412253,
			0, 0, 0,
			0,
			ind(fill, "emptyFill"), 1,
			ind(texture, "pcDesktop")
	endif

	define material "buttons" 20,
		0.1, 0.1, 0.1,
		0.6, 0.95, 1, 0, 5, 0,
		1, 1, 1,
		0, 0, 0,
		0, 0, 1,
		0

	!! END OF DEFINE MATERIALS !!

	add a/2, b/2, 0

	add 0, 0, standHeight
	rotx 90-displayAng
	add 0, -standCutRad+backPanelThk+0.01, displayThk+middlePanelThk+backPanelThk/2

	group "display"
		!!! Display
		add 0, 0, -displayThk
		material frameMat
		prism_ 10, displayThk,
			-displayWidth/2, 0, 15,
			displayWidth/2, 0, 15,
			displayWidth/2, displayHeight, 15,
			-displayWidth/2, displayHeight, 15,
			-displayWidth/2, 0, -1,

			-screenSizeX/2, screenFrameWidthBottom, 15,
			screenSizeX/2, screenFrameWidthBottom, 15,
			screenSizeX/2, screenSizeY+screenFrameWidthBottom, 15,
			-screenSizeX/2, screenSizeY+screenFrameWidthBottom, 15,
			-screenSizeX/2, screenFrameWidthBottom, -1
		del 1
	endgroup

	group "screen"
		!!! Screen
		add 0, screenFrameWidthBottom, -displayThk
		if pic then
			if gs_picture_custom and gs_picture_name <> "" then
				material "custDesktop"
			else
				material "pcdesktop"
			endif
		else
			material screenMat
		endif
		prism_ 4, screenThk+(GLOB_CONTEXT=5)*0.01,
			-screenSizeX/2, 0, 15,
			screenSizeX/2, 0, 15,
			screenSizeX/2, screenSizeY, 15,
			-screenSizeX/2, screenSizeY, 15
		del 1
		base
		muly 1
		add -screenSizeX/2, screenFrameWidthBottom, 0
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1
		coor 2+256, -1, -2, -3, -4
		body 1
		del 2
	endgroup

	group "middlepanel"
		!!! Middle Panel
		add 0, 0, -displayThk-middlePanelThk
		material frameMat
		prism_ 5, middlePanelThk,
			-displayWidth/2, 0, 15,
			displayWidth/2, 0, 15,
			displayWidth/2, displayHeight, 15,
			-displayWidth/2, displayHeight, 15,
			-displayWidth/2, 0, -1
		del 1
	endgroup

	group "backpanel"
		!!! Back Panel
		add 0, 0, -displayThk-middlePanelThk-backPanelThk
		material frameMat
		prism_ 5, backPanelThk,
			-displayWidth/2, 0, 15,
			displayWidth/2, 0, 15,
			displayWidth/2, displayHeight, 15,
			-displayWidth/2, displayHeight, 15,
			-displayWidth/2, 0, -1
		del 1
	endgroup

	group "backTopArchCut"
		ress=(512 * (gs_detlevel_3D_m = 2) + 8 * (gs_detlevel_3D_m = 1))/360*gamma
		if ress-int(ress)<0.5 then
			ress=int(ress)
			beta=gamma/ress
		else
			ress=int(ress)+1
			beta=gamma/ress
		endif

		alpha=delta
		for ii=1 to ress
			put -cos(delta)*backTopArchCutRad+backTopArchCutRad*cos(alpha), -sin(delta)*backTopArchCutRad+backTopArchCutRad*sin(alpha), 0, 0
			alpha=alpha-beta
		next ii

		add -displayWidth/2, backTopArchCutYpos, -displayThk-middlePanelThk-backPanelThk
		rotz 90
		material frameMat
		tube 5, ress+3, 1+2+16+32,
			1, backTopArchCuThk, 0,
			backTopArchCutWidth, backTopArchCuThk, 0,
			-1, 0, 800,
			0, 0, 1000,
			1, 0, 0,

			-cos(delta)*backTopArchCutRad+backTopArchCutRad*cos(delta), -sin(delta+beta)*backTopArchCutRad+backTopArchCutRad*sin(delta+beta), 0, 0,
			get ((ress)*4),
			-cos(delta)*backTopArchCutRad+backTopArchCutRad*cos(alpha), -sin(delta)*backTopArchCutRad+backTopArchCutRad*sin(alpha), 0, 0,
			-cos(delta)*backTopArchCutRad+backTopArchCutRad*cos(alpha-beta), -sin(delta)*backTopArchCutRad+backTopArchCutRad*sin(alpha-beta), 0, 0
		del 2
	endgroup

	group "standcut"
		add 0, 0, -displayThk-middlePanelThk-backPanelThk
		material frameMat
		prism_ 6, backPanelThk,
			-standCutWidth/2, 0, 79,
			0, 1, 800,
			-standCutWidth/2+standCutRad, standCutRad, 1079,
			standCutWidth/2-standCutRad, standCutRad, 79,
			1, 0, 800,
			standCutWidth/2, 0, 1079
		del 1
	endgroup

	group "standbox"
		add -standBoxWidth/2, standCutRad-backPanelThk-0.01, -displayThk-middlePanelThk
		roty 90
		material frameMat
		prism_ 4, standBoxWidth,
			0, 0, 15,
			0, backPanelThk, 79,
			1, 0, 800,
			backPanelThk, 0, 1079
		del 2
	endgroup

	group "buttons"
		material "buttons"
		!!! Buttons
		resol 128 * (gs_detlevel_3D_m = 2) + 32 * (gs_detlevel_3D_m = 1)
		add 0, -bottomDisplayArchCutHeight2/2, -displayThk/2
		cutpolya 8, 1, 0,
			-displayWidth/2, -10, 15,
			-displayWidth/2, bottomDisplayArchCutHeight1, 15,
			1, 0, 800,
			-displayWidth*0.25, (bottomDisplayArchCutHeight1-bottomDisplayArchCutHeight2)/2+bottomDisplayArchCutHeight2, 1015,
			0, bottomDisplayArchCutHeight2, 1015,
			displayWidth*0.25, (bottomDisplayArchCutHeight1-bottomDisplayArchCutHeight2)/2+bottomDisplayArchCutHeight2, 1015,
			displayWidth/2, bottomDisplayArchCutHeight1, 1015,
			displayWidth/2, -10, 15
		del 1

		add 0, 0, -displayThk/2
		cutpolya 8, 1, 0,
			-displayWidth/2, 10, 15,
			-displayWidth/2, bottomDisplayArchCutHeight1, 15,
			1, 0, 800,
			-displayWidth*0.25, (bottomDisplayArchCutHeight1-bottomDisplayArchCutHeight2)/2+bottomDisplayArchCutHeight2, 1015,
			0, bottomDisplayArchCutHeight2, 1015,
			displayWidth*0.25, (bottomDisplayArchCutHeight1-bottomDisplayArchCutHeight2)/2+bottomDisplayArchCutHeight2, 1015,
			displayWidth/2, bottomDisplayArchCutHeight1, 1015,
			displayWidth/2, 10, 15
		del 1

		add 0, 0, -displayThk/2
		for bt=1 to 5
		prism_ 4, displayThk,
			displayWidth*0.25, (bottomDisplayArchCutHeight1-bottomDisplayArchCutHeight2)/2+bottomDisplayArchCutHeight2, 15,
			displayWidth/2, bottomDisplayArchCutHeight1-bottomDisplayArchCutRad, 900,
			0, -buttonAngle, 4015,
			displayWidth/2, bottomDisplayArchCutHeight1-bottomDisplayArchCutRad, 15

		add displayWidth/2, bottomDisplayArchCutHeight1-bottomDisplayArchCutRad, 0
		rotz -buttonAngle-buttonSpaceAngle
		add -displayWidth/2, -bottomDisplayArchCutHeight1+bottomDisplayArchCutRad, 0
		next bt
		del 5*3
		del 1

		cutend
		cutend
	endgroup



	!!! Bottom Arch Cut Display
	resol 128 * (gs_detlevel_3D_m = 2) + 32 * (gs_detlevel_3D_m = 1)
	add 0, 0, -displayThk/2
	cutform 8, 1, 1+16,
		0, 0, -1, 0,
		-displayWidth/2, -10, 1+2+4+8+16+64,
		-displayWidth/2, bottomDisplayArchCutHeight1, 1+2+4+8+16+64,
		1, 0, 800,
		-displayWidth*0.25, (bottomDisplayArchCutHeight1-bottomDisplayArchCutHeight2)/2+bottomDisplayArchCutHeight2, 1001+2+4+8+16+64,
		0, bottomDisplayArchCutHeight2, 1001+2+4+8+16+64,
		displayWidth*0.25, (bottomDisplayArchCutHeight1-bottomDisplayArchCutHeight2)/2+bottomDisplayArchCutHeight2, 1001+2+4+8+16+64,
		displayWidth/2, bottomDisplayArchCutHeight1, 1001+2+4+8+16+64,
		displayWidth/2, -10, 15
	del 1

	!!! Bottom Arch Cut
	resol 128 * (gs_detlevel_3D_m = 2) + 32 * (gs_detlevel_3D_m = 1)
	add 0, 0, -displayThk-middlePanelThk-backPanelThk
	cutpolya 6, 1, 0,
		0, bottomArchCutRad, 979,
		-displayWidth/2, bottomArchCutHeight, 79,
		0, 0, 3079,
		displayWidth/2, bottomArchCutHeight, 3079,
		displayWidth/2, -10, 15,
		-displayWidth/2, -10, 15
	del 1

	!!! Side Arch Cut
	resol 128 * (gs_detlevel_3D_m = 2) + 32 * (gs_detlevel_3D_m = 1)
	add -displayWidth/2, 0, -displayThk
	for ix=1 to 2
		roty 90
		rotx 90
		cutpolya 6, 1, 0,
			0, 0, 79,
			1, 0, 800,
			sideArchCutWidth, sideArchCutHeight, 1015,
			sideArchCutWidth, -0.1, 15,
			0, -0.1, 15,
			0, 0, 15
		del 2
	addx displayWidth
	mulx -1
	next ix
	del 4
	del 1

	resol 128 * (gs_detlevel_3D_m = 2) + 32 * (gs_detlevel_3D_m = 1)
	add -displayWidth/2, bottomArchCutHeight, -displayThk
	for iy=1 to 2
		roty 90
		cutpolya 6, 1, 0,
			0, 0, 79,
			1, 0, 800,
			sideArchCutWidth, sideArchCutHeight, 1015,
			sideArchCutWidth, -0.1, 15,
			0, -0.1, 15,
			0, 0, 15
		del 1
	addy displayHeight-bottomArchCutHeight
	muly -1
	next iy
	del 4
	del 1

	!!! Back Arch Cut
	resol 128 * (gs_detlevel_3D_m = 2) + 8 * (gs_detlevel_3D_m = 1)
	add 0, 0, -displayThk-middlePanelThk-backPanelThk
	rotx 90
	cutpolya 6, 1, 0,
		0, backArchCutRad, 979,
		-displayWidth/2, backArchCutHeight, 79,
		0, 0, 3079,
		displayWidth/2, backArchCutHeight, 3079,
		displayWidth/2, -10, 15,
		-displayWidth/2, -10, 15
	del 2



	placegroup "screen"
	placegroup "display"
	placegroup "middlepanel"
	placegroup subgroup(subgroup("backpanel", "standcut"), "backTopArchCut")

	cutend
	cutend
	cutend
	cutend
	cutend
	cutend
	cutend

	placegroup "standbox"
	placegroup "buttons"

	del 3



	group "stand"
		material standMat
		resol gs_resol * (gs_detlevel_3D_m = 2) + 8 * (gs_detlevel_3D_m = 1)

		add 0, 0, 0
		mulx 1.5
		roty -90
		revolve 6, 360, 1+2,
			0, standRadBottom, 0,
			0.005, standRadBottom, 1,
			0.005, standRadTop+standRadBottom*0.4, 1,
			0, -1, 801,
			0.005+standRadBottom*0.4, standRadTop, 1001,
			standHeight+0.01, standRadTop, 1
	endgroup

	placegroup "stand"

	del 1

	end

endif





]]></Script_3D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_2D SectVersion="20" SectionFlags="0" SubIdent="0"><![CDATA[

pen gs_cont_pen

if iMonitorType = 1 then

	if NOT(bPartOfLayout) then CALL "Resize_A_B_ZZYZX" Parameters ALL

	add2 0,b/2-.015
	rect2 0,0,a,.03
	line2 0,.03,.06,.06
	line2 a,.03,a-.06,.06
	line2 .06,.06,a-.06,.06
	del 1

	add2 a/4,0
	rect2 0,0,a/2,b/2-.015
	rect2 0,b/2+.045,a/2,b
	line2 a/6,b/2+.045,a/6,b
	line2 a*2/6,b/2+.045,a*2/6,b

	del 1
	fill gs_fill_type

		POLY2_b 15, 2,gs_fill_pen,gs_back_pen,
				0,b/2-.015,0,
				0,b/2+.015,0,
				.06,b/2+.045,0,
				a/4,b/2+.045,0,
				a/4,b,0,
				a*3/4,b,0,
				a*3/4,b/2+.045,0,
				a-.06,b/2+.045,0,
				a,b/2+.015,0,
				a,b/2-.015,0,
				a*3/4,b/2-.015,0,
				a*3/4,0,0,
				a/4,0,0,
				a/4,b/2-.015,0,
				0,b/2-.015,-1

	if NOT(bPartOfLayout) then
		HOTSPOT2 a/2,b
		HOTSPOT2 a/2,b/2
	endif

endif

if iMonitorType = 2 then

	add2 a/2, b/2

	hotspot2 -a/2, -b/2
	hotspot2 a/2, -b/2
	hotspot2 -a/2, b/2
	hotspot2 a/2, b/2

	hotspot2 0, 0
	hotspot2 0, -b/2
	hotspot2 0, b/2

	add2 0, -displayThk-middlePanelThk-backPanelThk/2
		hotspot2 -a/2, 0
		hotspot2 a/2, 0
	del 1

	!!! *** Background Fill
	fill gs_fill_type

	mul2 1.5, 1
	POLY2_B       2,      1+2+4,      gs_fill_pen,     gs_back_pen,
		0, 0, 901,
		b/2, 360, 4001
	del 1

	add2 0, -displayThk-middlePanelThk-backPanelThk/2
	POLY2_B       6,      1+2+4,      gs_fill_pen,     gs_back_pen,
		-a/2, 0, 1,
		a/2, 0, 1,
		a/2, displayThk+middlePanelThk+backPanelThk-backArchCutHeight, 1,
		0, -backArchCutRad, 901,
		0, displayThk+middlePanelThk+backPanelThk, 3001,
		-a/2, displayThk+middlePanelThk+backPanelThk-backArchCutHeight, 3001
	del 1

	del 1

endif


]]></Script_2D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_1D SectVersion="20" SectionFlags="0" SubIdent="0"><![CDATA[

EPS = 0.0001

SCREEN_43	= 1
SCREEN_169	= 2
SCREEN_239	= 3
SCREEN_CUST	= 4


DIM stMonitorType[2]
	stMonitorType[1]=`Type 1`
	stMonitorType[2]=`Type 2`

if iMonitorType = 2 then

	if iScreenSize = 1 then screenSizeDiagonal = 15"
	if iScreenSize = 2 then screenSizeDiagonal = 17"
	if iScreenSize = 3 then screenSizeDiagonal = 19"
	if iScreenSize = 4 then screenSizeDiagonal = 21"
	if iScreenSize = 5 then screenSizeDiagonal = 23"
	if iScreenSize = 6 then screenSizeDiagonal = 25"
	if iScreenSize = 7 then screenSizeDiagonal = 27"
	if iScreenSize = 8 then screenSizeDiagonal = screenSize

	if iScreenRatio = 1 then alpha = atn(3/4)
	if iScreenRatio = 2 then alpha = atn(9/16)
	if iScreenRatio = 3 then alpha = atn(1/2.39)
	if iScreenRatio = 4 then alpha = atn(1/screenRatio)

	screenSizeX	= cos(alpha) * screenSizeDiagonal
	screenSizeY	= sin(alpha) * screenSizeDiagonal

	screenThk				= 0.003
	screenFrameWidthBottom	= 0.045
	screenFrameWidthSide	= 0.02
	screenFrameWidthTop		= 0.02

	displayThk				= 0.005
	displayWidth			= screenSizeX + screenFrameWidthSide * 2
	displayHeight			= screenSizeY + screenFrameWidthBottom + screenFrameWidthTop

	middlePanelThk			= 0.01
	backPanelThk			= 0.03

	sideArchCutWidth		= middlePanelThk + backPanelThk
	sideArchCutHeight		= 0.025

	backArchCutHeight		= 0.025
	backArchCutRad			= (backArchCutHeight**2+(displayWidth/2)**2)/(2*backArchCutHeight)
	bottomArchCutHeight		= 0.01
	bottomArchCutRad		= (bottomArchCutHeight**2+(displayWidth/2)**2)/(2*bottomArchCutHeight)

	backTopArchCutHeight	= 0.03
	backTopArchCuThk		= 0.025
	backTopArchCutWidth		= 0.05
	backTopArchCutRad		= (backTopArchCutHeight**2+(displayWidth/2)**2)/(2*backTopArchCutHeight)
	backTopArchCutYpos		= displayHeight - displayHeight / 3
	backTopArchCutZpos		= 0.0

	delta=asn(displayWidth/2/backTopArchCutRad)
	gamma=delta*2

	standCutWidth			= displayWidth * 2 / 3
	standCutRad				= 0.06

	standHeight				= 0.06
	standRadBottom			= displayWidth / 6		!0.08
	standRadTop				= 0.01

	standPlateRadX			= 0.13
	standPlateRadY			= 0.07
	standPlateRadZ			= 0.05

	standBoxWidth			= 0.08

	bottomDisplayArchCutHeight1	= 0.02
	bottomDisplayArchCutHeight2	= 0.005
	ddd							=(bottomDisplayArchCutHeight1-bottomDisplayArchCutHeight2)/2
	bottomDisplayArchCutRad		= (ddd**2+(displayWidth/4)**2)/(2*ddd)
	teta=asn(displayWidth/4/bottomDisplayArchCutRad)

	buttonAngle=0.7
	buttonSpaceAngle=0.15

	a						= displayWidth
	parameters a			= a
	b						= standRadBottom * 2
	parameters b			= b
	zzyzx					= displayHeight-standCutRad+backPanelThk+0.01+standHeight
	parameters zzyzx		= zzyzx

endif



]]></Script_1D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_PR SectVersion="20" SectionFlags="0" SubIdent="0"><![CDATA[

]]></Script_PR>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_UI SectVersion="20" SectionFlags="0" SubIdent="0"><![CDATA[

]]></Script_UI>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_VL SectVersion="20" SectionFlags="0" SubIdent="0"><![CDATA[

call "FM_types_OE_WMCC" parameters all

values "monitorType" stMonitorType
values "iMonitorType" 1, 2
if GLOB_MODPAR_NAME = "monitorType" then
	if monitorType = stMonitorType[1]	then iMonitorType = 1
	if monitorType = stMonitorType[2]	then iMonitorType = 2
	parameters iMonitorType = iMonitorType
else
	if iMonitorType = 1 then	monitorType = stMonitorType[1]
	if iMonitorType = 2 then	monitorType = stMonitorType[2]
	parameters monitorType = monitorType
endif

values "gs_detlevel_3D" `Detailed`, `Simple`, `Off`
values "gs_detlevel_3D_m" 2, 1, 0
if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	if gs_detlevel_3D = `Detailed`	then gs_detlevel_3D_m = 2
	if gs_detlevel_3D = `Simple`	then gs_detlevel_3D_m = 1
	if gs_detlevel_3D = `Off`		then gs_detlevel_3D_m = 0
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	if gs_detlevel_3D_m = 2 then	gs_detlevel_3D = `Detailed`
	if gs_detlevel_3D_m = 1 then	gs_detlevel_3D = `Simple`
	if gs_detlevel_3D_m = 0 then	gs_detlevel_3D = `Off`
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

if gs_detlevel_3D_m = 1 then hideparameter "gs_resol"
if gs_detlevel_3D_m = 0 then hideparameter "gs_resol", "gs_shadow"



if iMonitorType = 1 then

	lock "st_screenSize", "displayAng", "screenMat", "frameMat", "standMat"
	hideparameter "st_screenSize", "displayAng", "screenMat", "frameMat", "standMat"

	values "A" range [3*(0.025+0.035), )
	values "zzyzx" range [0.2, )
	values "gs_resol" RANGE [3,)

	values{2} "iScreenRatio"	SCREEN_43,		"4:3",
								SCREEN_169,		"16:9",
								SCREEN_239,		"2.39:1",
								SCREEN_CUST,	`Custom`

	values "screenSize" range (0,)

	if iScreenRatio <> 4 then
		if iScreenRatio	= SCREEN_43		then screenRatioRate = 4/3
		if iScreenRatio	= SCREEN_169	then screenRatioRate = 16/9
		if iScreenRatio	= SCREEN_239	then screenRatioRate = 2.39

		parameters screenRatioRate = screenRatioRate
		lock "screenRatioRate"
	endif

	IF gs_cont_pen=0 THEN PARAMETERS gs_cont_pen=10
	IF gs_fill_pen=0 THEN PARAMETERS gs_fill_pen=91

	frameThickness = 0.015
	bottomFrameThickness = 0.015
	tp = zzyzx/7
	if bUnitSizing then
		lock "A"
		lock "zzyzx"
		A = screenSize * cos(atn(1/screenRatioRate)) + 2*frameThickness
		zzyzx = screenSize * sin(atn(1/screenRatioRate)) + frameThickness + bottomFrameThickness + tp
		parameters A = A , zzyzx = zzyzx
	else
		screenSize = sqr((A - 2*frameThickness)^2 + (ZZYZX - frameThickness - bottomFrameThickness - tp)^2)
		screenRatioRate = (A - 2*frameThickness) / (ZZYZX - frameThickness - bottomFrameThickness - tp)
		lock "screenSize"
		lock "screenRatioRate"
		lock "iScreenRatio"
		parameters screenSize = screenSize, screenRatioRate = screenRatioRate
	endif

	IF pic=1 THEN HIDEPARAMETER "dispmat"
	IF gs_picture_custom=0 THEN HIDEPARAMETER "gs_picture_name"

endif

if iMonitorType = 2 then

	lock "bUnitSizing", "box_mat", "feetMat", "dispmat"
	hideparameter "bUnitSizing", "box_mat", "feetMat", "dispmat"

	values "gs_resol" range [3,)

	DIM stScreenSize[7]
		stScreenSize[1]=`15-inch`
		stScreenSize[2]=`17-inch`
		stScreenSize[3]=`19-inch`
		stScreenSize[4]=`21-inch`
		stScreenSize[5]=`23-inch`
		stScreenSize[6]=`25-inch`
		stScreenSize[7]=`27-inch`

	values "st_screenSize" stScreenSize
	values "iScreenSize" 1, 2, 3, 4, 5, 6, 7
	if GLOB_MODPAR_NAME = "st_screenSize" then
		iScreenSize = 1
		for i = 2 to vardim1(stScreenSize)
			if st_screenSize = stScreenSize[i]	then iScreenSize = i
		next i
		parameters iScreenSize = iScreenSize
	else
		st_screenSize = stScreenSize[max(min(iScreenSize, vardim1(stScreenSize)), 1)]
		parameters st_screenSize = st_screenSize
	endif

	if iScreenSize <> 8 then
		lock "screenSize"
		hideparameter "screenSize"
	endif

	values "screenSize" range (screenFrameWidthSide*3,)

	values{2} "iScreenRatio"	SCREEN_43,		"4:3",
								SCREEN_169,		"16:9",
								SCREEN_239,		"2.39:1"

	if iScreenRatio	= SCREEN_43		then screenRatioRate = 4/3
	if iScreenRatio	= SCREEN_169	then screenRatioRate = 16/9
	if iScreenRatio	= SCREEN_239	then screenRatioRate = 2.39

	parameters screenRatioRate = screenRatioRate
	lock "screenRatioRate"

	lock "a", "b", "zzyzx"

	if pic then
		lock "screenMat"
		hideparameter "screenMat"
	else
		lock "gs_picture_custom", "gs_picture_name"
		hideparameter "gs_picture_custom", "gs_picture_name"
	endif
	if not(gs_picture_custom) then
		lock "gs_picture_name"
		hideparameter "gs_picture_name"
	endif

endif

]]></Script_VL>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Comment SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Comment>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_FWM SectVersion="20" SectionFlags="0" SubIdent="0"><![CDATA[

]]></Script_FWM>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_BWM SectVersion="20" SectionFlags="0" SubIdent="0"><![CDATA[

]]></Script_BWM>

<Copyright SectVersion="1" SectionFlags="0" SubIdent="0"><Author>BIMobject</Author><License><Type>CC BY-ND</Type><Version>3.0</Version></License></Copyright><ParamSection SectVersion="22" SectionFlags="0" SubIdent="0">
	<ParamSectHeader>
		<Version>22</Version>
		<AutoHotspots>no</AutoHotspots>
		<StatBits>
			<STBit_FixSize/>
		</StatBits>
		<WDLeftFrame>0</WDLeftFrame>
		<WDRightFrame>0</WDRightFrame>
		<WDTopFrame>0</WDTopFrame>
		<WDBotFrame>0</WDBotFrame>
		<LayFlags>65535</LayFlags>
		<WDMirrorThickness>0</WDMirrorThickness>
		<WDWallInset>0</WDWallInset>
		<SymbolColor>0</SymbolColor>
	</ParamSectHeader>
	<Parameters>
		<Length Name="A">
			<Description><![CDATA["Dimension 1"]]></Description>
			<Value>0.5048984</Value>
		</Length>
		<Length Name="B">
			<Description><![CDATA["Dimension 2"]]></Description>
			<Value>0.1682995</Value>
		</Length>
		<Length Name="ZZYZX">
			<Description><![CDATA["Height"]]></Description>
			<Value>0.3665053</Value>
		</Length>
		<String Name="monitorType">
			<Description><![CDATA["Monitor Type"]]></Description>
			<Value><![CDATA["Type 2"]]></Value>
		</String>
		<Integer Name="iMonitorType">
			<Description><![CDATA["Monitor Type"]]></Description>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>2</Value>
		</Integer>
		<String Name="gs_detlevel_3D">
			<Description><![CDATA["3D Detail Level"]]></Description>
			<Value><![CDATA["Detailed"]]></Value>
		</String>
		<Integer Name="gs_detlevel_3D_m">
			<Description><![CDATA["3D"]]></Description>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>2</Value>
		</Integer>
		<String Name="st_screenSize">
			<Description><![CDATA["Screen Size"]]></Description>
			<Value><![CDATA["21-inch"]]></Value>
		</String>
		<Integer Name="iScreenSize">
			<Description><![CDATA["Screen Size"]]></Description>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>4</Value>
		</Integer>
		<Boolean Name="AC_show2DHotspotsIn3D">
			<Description><![CDATA["Show 2D Hotspots in 3D"]]></Description>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>1</Value>
		</Boolean>
		<Length Name="ac_bottomlevel">
			<Description><![CDATA["Bottom Level"]]></Description>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>1</Value>
		</Length>
		<Length Name="ac_toplevel">
			<Description><![CDATA["Top Level"]]></Description>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0</Value>
		</Length>
		<Boolean Name="bUnitSizing">
			<Description><![CDATA["Change Screen Size"]]></Description>
			<Value>0</Value>
		</Boolean>
		<Length Name="screenSize">
			<Description><![CDATA["Screen Size"]]></Description>
			<Value>0.4580393</Value>
		</Length>
		<Integer Name="iScreenRatio">
			<Description><![CDATA["Screen Ratio"]]></Description>
			<Value>2</Value>
		</Integer>
		<RealNum Name="screenRatioRate">
			<Description><![CDATA["Screen Ratio Rate"]]></Description>
			<Value>1.3703704</Value>
		</RealNum>
		<Angle Name="displayAng">
			<Description><![CDATA["Display Angle"]]></Description>
			<Value>0</Value>
		</Angle>

		<!-- gs_picture: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="gs_picture">
			<Description><![CDATA["Picture"]]></Description>
		</Title>
		<Boolean Name="pic">
			<Description><![CDATA["Picture Map"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>1</Value>
		</Boolean>
		<Boolean Name="gs_picture_custom">
			<Description><![CDATA["Use Custom Picture"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0</Value>
		</Boolean>
		<String Name="gs_picture_name">
			<Description><![CDATA["Custom Picture Name"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>

		<!-- gs_2D_representation: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="gs_2D_representation">
			<Description><![CDATA["2D Representation"]]></Description>
		</Title>
		<PenColor Name="gs_cont_pen">
			<Description><![CDATA["Contour Pen"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>4</Value>
		</PenColor>
		<FillPattern Name="gs_fill_type">
			<Description><![CDATA["Fill Type"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>65</Value>
		</FillPattern>
		<PenColor Name="gs_fill_pen">
			<Description><![CDATA["Fill Pen"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>19</Value>
		</PenColor>
		<PenColor Name="gs_back_pen">
			<Description><![CDATA["Fill Background Pen"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>19</Value>
		</PenColor>

		<!-- gs_3D_representation: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="gs_3D_representation">
			<Description><![CDATA["3D Representation"]]></Description>
		</Title>
		<Boolean Name="gs_shadow">
			<Description><![CDATA["Shadow Casting"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>1</Value>
		</Boolean>
		<Integer Name="gs_resol">
			<Description><![CDATA["Resolution"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>24</Value>
		</Integer>

		<!-- gs_material: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="gs_material">
			<Description><![CDATA["Surfaces"]]></Description>
		</Title>
		<Material Name="screenMat">
			<Description><![CDATA["Screen"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>29</Value>
		</Material>
		<Material Name="frameMat">
			<Description><![CDATA["Frame"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>49</Value>
		</Material>
		<Material Name="standMat">
			<Description><![CDATA["Stand"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>49</Value>
		</Material>
		<Material Name="box_mat">
			<Description><![CDATA["Box"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>18</Value>
		</Material>
		<Material Name="feetmat">
			<Description><![CDATA["Feet"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>99</Value>
		</Material>
		<Material Name="dispmat">
			<Description><![CDATA["Display"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>29</Value>
		</Material>

		<!-- gs_list: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="gs_list">
			<Description><![CDATA["Parameters for Listing"]]></Description>
		</Title>
		<RealNum Name="gs_list_cost">
			<Description><![CDATA["Cost"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0</Value>
		</RealNum>
		<String Name="gs_list_manufacturer">
			<Description><![CDATA["Manufacturer"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="gs_list_note">
			<Description><![CDATA["Note/Remarks"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="gs_list_location">
			<Description><![CDATA["Location"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="gs_list_accessories">
			<Description><![CDATA["Accessories"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<RealNum Name="gs_list_volts">
			<Description><![CDATA["Volts"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0</Value>
		</RealNum>
		<String Name="FM_Type">
			<Description><![CDATA["Group Type"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA["Electric Facilities"]]></Value>
		</String>
		<Integer Name="iFMType">
			<Description><![CDATA["Group Type"]]></Description>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_Hidden/>
			</Flags>
			<Value>8</Value>
		</Integer>
		<String Name="FM_InventoryNumber">
			<Description><![CDATA["Inventory Number"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="FM_SerialNumber">
			<Description><![CDATA["Serial Number"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="FM_ProductionYear">
			<Description><![CDATA["Production Year"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<RealNum Name="FM_ObjectWeight">
			<Description><![CDATA["Object Weight"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0</Value>
		</RealNum>
		<String Name="FM_ObjectWeightUnit">
			<Description><![CDATA["Object Weight Unit"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA["kg"]]></Value>
		</String>
		<RealNum Name="FM_PowerConsumption">
			<Description><![CDATA["Wattage"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0</Value>
		</RealNum>
		<RealNum Name="FM_Egress">
			<Description><![CDATA["Egress"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0</Value>
		</RealNum>
		<String Name="gs_list_custom1">
			<Description><![CDATA["User Defined 1"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="gs_list_custom2">
			<Description><![CDATA["User Defined 2"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="gs_list_custom3">
			<Description><![CDATA["User Defined 3"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="gs_list_custom4">
			<Description><![CDATA["User Defined 4"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="gs_list_custom5">
			<Description><![CDATA["User Defined 5"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<Boolean Name="bPartOfLayout">
			<Description><![CDATA["Part Of Layout"]]></Description>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0</Value>
		</Boolean>
	</Parameters>
</ParamSection>
</Symbol>
